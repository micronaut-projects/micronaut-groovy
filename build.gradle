buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "io.micronaut.build.internal:micronaut-gradle-plugins:4.1.1"
    }
}

subprojects { Project subproject ->
    group "io.micronaut.groovy"

    repositories {
        mavenCentral()
        maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://repo.grails.org/grails/core" }
    }

    if (subproject.name != 'groovy-bom') {
        apply plugin: "io.micronaut.build.internal.common"

        micronautBuild {
            enableBom = false
        }

    } else {
        apply plugin: "java-platform"
        version projectVersion
    }

    apply plugin: "io.micronaut.build.internal.publishing"
    apply plugin: "io.micronaut.build.internal.dependency-updates"

    if (subproject.name != 'groovy-bom') {

        configurations {
            documentation
        }

        dependencies {
            documentation ("com.github.javaparser:javaparser-core:3.23.0")
            testImplementation "org.codehaus.groovy:groovy-test:$groovyVersion"
            testImplementation("org.spockframework:spock-core:$spockVersion") {
                exclude module:'groovy-all'
            }
            testImplementation("io.micronaut.test:micronaut-test-spock:$micronautTestVersion") {
                exclude module: 'micronaut-bom'
                exclude module: 'micronaut-runtime'
            }
            testImplementation("io.micronaut:micronaut-runtime:$micronautVersion")
        }

        groovydoc {
            classpath += project.configurations.documentation
        }
    }
}

apply plugin: "io.micronaut.build.internal.docs"
apply plugin: "io.micronaut.build.internal.dependency-updates"
