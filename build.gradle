buildscript {
    repositories {
        gradlePluginPortal()
        jcenter()
        maven { url "https://dl.bintray.com/micronaut/core-releases-local" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "io.micronaut.build:micronaut-gradle-plugins:2.0.12"
    }
}

subprojects { Project subproject ->
    group "io.micronaut.groovy"

    repositories {
        jcenter()
    }

    if (subproject.name != 'groovy-bom') {
        apply plugin: "io.micronaut.build.common"

        micronautBuild {
            enableBom = false
        }

    } else {
        apply plugin: "java-platform"
        version projectVersion
    }
    
    apply plugin: "io.micronaut.build.publishing"
    apply plugin: "io.micronaut.build.dependency-updates"   

    if (subproject.name != 'groovy-bom') {
        dependencies {
            documentation ("com.github.javaparser:javaparser-core:3.18.0")
            testImplementation "org.codehaus.groovy:groovy-test:$groovyVersion"
            testImplementation("org.spockframework:spock-core:${spockVersion}") {
                exclude module:'groovy-all'
            }
            testImplementation "io.micronaut.test:micronaut-test-spock:$micronautTestVersion"
        }
    }
}

apply plugin: "io.micronaut.build.docs"
apply plugin: "io.micronaut.build.dependency-updates"

